apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlinx-serialization'

ext {
    extName = 'AIOStreams'
    extClass = '.AIOStreams'
    extVersionCode = 4
}

// Load keystore properties
def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
if (keystorePropertiesFile.exists()) {
    keystorePropertiesFile.withInputStream { keystoreProperties.load(it) }
}

android {
    compileSdk 34
    namespace "eu.kanade.tachiyomi.animeextension.all.aiostreams"

    sourceSets {
        main {
            manifest.srcFile "AndroidManifest.xml"
            java.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }
    }

    defaultConfig {
        applicationId "eu.kanade.tachiyomi.animeextension.all.aiostreams"
        minSdk 21
        targetSdk 34
        versionCode extVersionCode
        versionName "16.${extVersionCode}"
        
        manifestPlaceholders = [
            appName : "Aniyomi: ${extName}",
            extClass: extClass,
            nsfw    : 0,
        ]
    }

    signingConfigs {
        release {
            storeFile rootProject.file("aiostreams-release.jks")
            storePassword keystoreProperties['KEYSTORE_PASSWORD'] ?: System.getenv("KEYSTORE_PASSWORD")
            keyAlias "aiostreams"
            keyPassword keystoreProperties['KEY_PASSWORD'] ?: System.getenv("KEY_PASSWORD")
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
        }
        debug {
            minifyEnabled false
        }
    }

    buildFeatures {
        buildConfig true
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }
}

dependencies {
    implementation(project(":core"))
    compileOnly(libs.bundles.common)
}
